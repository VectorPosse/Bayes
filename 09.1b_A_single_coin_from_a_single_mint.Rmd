---
title: "9.1(b): A single coin from a single mint"
output: html_notebook
---


## Introduction

This is the second of two models in Section 9.1 of Kruschke. It makes a very specific claim about the value of $\omega$, but is less sure about the dependence of $\theta$ on $\omega$.


## Preliminaries

Load necessary packages:

```{r}
library(tidyverse)
library(rstan)
library(tidybayes)
library(bayesplot)
```

Set Stan to save compiled code.

```{r}
rstan_options(auto_write = TRUE)
```

Set Stan to use parallel processing where possible.

```{r}
options(mc.cores = parallel::detectCores())
```


## Data

```{r}
N <- 12
y <- c(rep(1, 9), rep(0, 3)) # 9 heads, 3 tails
y
```

```{r}
stan_data <- list(N = N, y = y)
```


## Prior

### Simulation code

This is the same code as in 9.1(a) except that the values of $A_{\omega}$, $B_{\omega}$, and $K$ are changed.

```{stan, output.var = "scsmb_prior", cache = TRUE}
data {
    int<lower = 0> N;
    array[N] int<lower = 0, upper = 1> y;
}
transformed data {
    real<lower = 0> A_omega;
    real<lower = 0> B_omega;
    real<lower = 0> K;

    A_omega = 20;   // hyperprior parameters
    B_omega = 20;
    K = 6;          // concentration
}
generated quantities {
    real<lower = 0, upper = 1> omega;
    real<lower = 0, upper = 1> theta;
    real<lower = 0> a;
    real<lower = 0> b;
    array[N] int<lower = 0, upper = 1> y_sim;
    
    omega = beta_rng(A_omega, B_omega); // mode
    a = omega * (K - 2) + 1;
    b = (1 - omega) * (K - 2) + 1;
    theta = beta_rng(a, b);  // probability of success
    
    for (n in 1:N) {
        y_sim[n] = bernoulli_rng(theta);
    }
}
```

```{r, cache = TRUE}
fit_scsmb_prior <- sampling(scsmb_prior,
                            data = stan_data,
                            chains = 1,
                            algorithm = "Fixed_param",
                            seed = 11111,
                            refresh = 0)
```

```{r}
samples_scsmb_prior <- tidy_draws(fit_scsmb_prior)
samples_scsmb_prior
```

### Examine prior

```{r}
mcmc_hist(samples_scsmb_prior,
          pars = c("omega", "theta"))
```

Now `omega` and `theta` are much less correlated:

```{r}
mcmc_pairs(fit_scsmb_prior,
           pars = c("omega", "theta"))
```

### Prior predictive distribution

```{r}
y_sim_scsmb_prior <- samples_scsmb_prior %>%
    dplyr::select(starts_with("y_sim")) %>%
    as.matrix()
```

```{r}
ppd_hist(ypred = y_sim_scsmb_prior[1:20,])
```


## Model

```{stan, output.var = "scsmb", cache = TRUE}
data {
    int<lower = 0> N;
    array[N] int<lower = 0, upper = 1> y;
}
transformed data {
    real<lower = 0> A_omega;  
    real<lower = 0> B_omega;
    real<lower = 0> K;        

    A_omega = 20;   // hyperprior parameters
    B_omega = 20;
    K = 6;          // concentration
}
parameters {
    real<lower = 0, upper = 1> omega;
    real<lower = 0, upper = 1> theta;
}
transformed parameters {
    real<lower = 0> a;
    real<lower = 0> b;
    
    a = omega * (K - 2) + 1;
    b = (1 - omega) * (K - 2) + 1;
}
model {
    omega ~ beta(A_omega, B_omega);  // hyperprior for mode
    theta ~ beta(a, b);              // prior prob of success
    y ~ bernoulli(theta);            // likelihood
}
generated quantities {
    array[N] int<lower = 0, upper = 1> y_sim;
    
    for (n in 1:N) {
        y_sim[n] = bernoulli_rng(theta);
    }
}
```

```{r, cache = TRUE}
fit_scsmb <- sampling(scsmb,
                      data = stan_data,
                      seed = 11111,
                      refresh = 0)
```

```{r}
samples_scsmb <- tidy_draws(fit_scsmb)
samples_scsmb
```


## Model diagnostics

```{r}
stan_trace(fit_scsmb,
           pars = c("omega", "theta"))
```

```{r}
mcmc_acf(fit_scsmb,
         pars = c("omega", "theta"))
```

```{r}
mcmc_rhat(rhat(fit_scsmb))
```

```{r}
mcmc_neff(neff_ratio(fit_scsmb))
```

## Model summary

```{r}
print(fit_scsmb,
      pars = c("omega", "theta"))
```


## Model visualization

```{r}
mcmc_areas(fit_scsmb, pars = c("omega", "theta"))
```

Note above that `theta` has been pulled closer to the data and father from `omega`.

```{r}
pairs(fit_scsmb,
      pars = c("omega", "theta"))
```


## Posterior predictive check

```{r}
y_sim_scsmb <- samples_scsmb %>%
    dplyr::select(starts_with("y_sim")) %>%
    as.matrix()
```

```{r}
ppc_hist(y = y,
         yrep = y_sim_scsmb[1:19, ])
```

```{r}
ppc_bars(y = y,
         yrep = y_sim_scsmb)
```

Note that, despite a very different hyperprior parameters, the bar graph and intervals above are almost identical to those in 9.1(a). Both models do a pretty good job recovering the original data. (The degree to which they don't is a function of the paucity of data, meaning that the priors play a stronger role.)
