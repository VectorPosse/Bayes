---
title: "9.1(b): A single coin from a single mint"
output: html_notebook
---


## Introduction

This is the second of two models in Section 9.1 of Kruschke. It makes a very specific claim about the value of $\omega$, but is less sure about the dependence of $\theta$ on $\omega$.


## Preliminaries

Load necessary packages:

```{r, message = FALSE}
library(rstan)
library(shinystan)
library(bayesplot)
```

Set Stan to save compiled code.

```{r}
rstan_options(auto_write = TRUE)
```

Set Stan to use parallel processing where possible.

```{r}
options(mc.cores = parallel::detectCores())
```


## Data

```{r}
N <- 12
y <- c(rep(1, 9), rep(0, 3)) # 9 heads, 3 tails
stan_data <- list(N = N, y = y)
```


## Stan code

This is the same code as in 9.1(a) except that the values of $A_{\omega}$, $B_{\omega}$, and $K$ are changed.

```{stan, output.var = "scsmb", cache = TRUE}
data {
    int<lower = 0> N;
    int<lower = 0, upper = 1> y[N];
}
transformed data {
    real<lower = 0> A_omega;
    real<lower = 0> B_omega;
    real<lower = 0> K;

    A_omega = 20;
    B_omega = 20;
    K = 6;
}
parameters {
    real<lower = 0, upper = 1> omega;
    real<lower = 0, upper = 1> theta;
}
transformed parameters {
    real<lower = 0> a;
    real<lower = 0> b;
    
    a = omega * (K - 2) + 1;
    b = (1 - omega) * (K - 2) + 1;
}
model {
    omega ~ beta(A_omega, B_omega);
    theta ~ beta(a, b);
    y ~ bernoulli(theta);
}
generated quantities {
    int<lower = 0, upper = 1> y_rep[N];
    
    for (n in 1:N) {
        y_rep[n] = bernoulli_rng(theta);
    }
}
```


## Sampling from the model

```{r}
set.seed(11111)
fit_scsmb <- sampling(scsmb, data = stan_data, refresh = 0)
```


## Diagnosing the model

```{r}
plot(fit_scsmb, plotfun = "ac", pars = c("omega", "theta"))
```

```{r}
plot(fit_scsmb, plotfun = "trace",
     pars = c("omega", "theta"))
```


## Summarizing the model

```{r}
fit_scsmb
```


## Visualizing the model

```{r}
pairs(fit_scsmb, pars = c("omega", "theta"))
```

```{r}
plot(fit_scsmb, pars = c("omega", "theta"))
```

In this example, the prior is not as strong, so the value of $\theta$ is closer to the data than to $\omega$.


## Examining the prior

```{stan, output.var = "scsmb_prior", cache = TRUE}
data {
    int<lower = 0> N;
    int<lower = 0, upper = 1> y[N];
}
transformed data {
    real<lower = 0> A_omega;
    real<lower = 0> B_omega;
    real<lower = 0> K;

    A_omega = 20;
    B_omega = 20;
    K = 6;
}
parameters {
    real<lower = 0, upper = 1> omega;
    real<lower = 0, upper = 1> theta;
}
transformed parameters {
    real<lower = 0> a;
    real<lower = 0> b;
    
    a = omega * (K - 2) + 1;
    b = (1 - omega) * (K - 2) + 1;
}
model {
    omega ~ beta(A_omega, B_omega);
    theta ~ beta(a, b);
//    y ~ bernoulli(theta);
}
```

```{r}
set.seed(11111)
fit_scsmb_prior <- sampling(scsmb_prior, data = stan_data, refresh = 0)
```

```{r}
fit_scsmb_prior
```

```{r}
pairs(fit_scsmb_prior, pars = c("omega", "theta"))
```

```{r}
plot(fit_scsmb_prior, pars = c("omega", "theta"))
```


## Posterior predictive check

Extract predicted values:

```{r}
samples_scsmb <- extract(fit_scsmb)
y_rep <- samples_scsmb$y_rep
```

Graph values of $y$ against summaries of $y_{rep}$:

```{r}
ppc_bars(y, y_rep)
```

Notice the shrinkage of the simulated posterior data relative to the original data.


## ShinyStan

Run the following code from the Console:

```
launch_shinystan(fit_scsmb)
```